#!/usr/bin/perl

$| = 1;

use strict;
use warnings;

no warnings ('recursion');

use FindBin;
use lib "$FindBin::RealBin/../libs";

use Amling::Git::Utils;

if(@ARGV < 1)
{
    die "Usage: $0 <main branch> <query commit>...";
}

my $master = shift;

my %commits;
my $master_commit;
{
    my $cb = sub
    {
        my $commit = shift;
        $master_commit = $commit->{'hash'} unless(defined($master_commit));
        $commits{$commit->{'hash'}} = $commit;
    };
    Amling::Git::Utils::log_commits([$master], $cb);
}

for my $query (@ARGV)
{
    my $path = find($master_commit, Amling::Git::Utils::convert_commitlike($query), {});
    if(!defined($path))
    {
        print "$query: ?\n";
    }
    else
    {
        print "$query:\n";
        for my $commit (@$path)
        {
            print "    $commit " . $commits{$commit}->{'subj'} . "\n";
        }
    }
}

sub find
{
    my $commit = shift;
    my $query_commit = shift;
    my $already_checked = shift;

    if($query_commit eq $commit)
    {
        return [$commit];
    }

    if($already_checked->{$commit})
    {
        return undef;
    }
    $already_checked->{$commit} = 1;

    my @parents = @{$commits{$commit}->{'parents'}};
    if(@parents)
    {
        my $path = find($parents[0], $query_commit, $already_checked);
        return $path if(defined($path));
    }

    for(my $i = 1; $i < @parents; ++$i)
    {
        my $path = find($parents[$i], $query_commit, $already_checked);
        return [$commit, @$path] if(defined($path));
    }

    return undef;
}
