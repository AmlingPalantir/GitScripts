#!/usr/bin/perl

$| = 1;

use strict;
use warnings;

use FindBin;
use lib "$FindBin::RealBin/../libs";

use Amling::Git::Utils;
use File::Temp ('tempdir');

if(@ARGV != 2)
{
    die "Usage: $0 LHS RHS\n";
}

{
    my $lhs = Amling::Git::Utils::convert_commitlike($ARGV[0]);
    my $rhs = Amling::Git::Utils::convert_commitlike($ARGV[1]);

    my %parent_child_1;
    my %child_parent_1;
    my %lhs_commits;
    my %rhs_commits;
    for my $tuple ([$rhs, $lhs, \%lhs_commits], [$lhs, $rhs, \%rhs_commits])
    {
        my ($from, $to, $commits) = @$tuple;

        my $cb = sub
        {
            my $h = shift;
            $commits->{$h->{'hash'}} = $h;
            for my $parent (@{$h->{'parents'}})
            {
                ($parent_child_1{$parent} ||= {})->{$h->{'hash'}} = 1;
                ($child_parent_1{$h->{'hash'}} ||= {})->{$parent} = 1;
            }
        };
        Amling::Git::Utils::log_commits(["$from..$to"], $cb);
    }

    my %min_bad;
    {
        my @queue;

        for my $lhs_commit (keys(%lhs_commits))
        {
            for my $rhs_commit (keys(%rhs_commits))
            {
                push @queue, [$lhs_commit, $rhs_commit];
            }
        }

        my %bad;
        my %good;
        #@queue = (map { $_->[0] } sort { $a->[1] <=> $b->[1] } map { [$_, rand()] } @queue);
        @queue = sort { ($a->[0] cmp $b->[0]) || ($a->[1] cmp $b->[1]) } @queue;
        while(@queue)
        {
            my ($lhs_commit, $rhs_commit) = @{shift(@queue)};
            my $k = "$lhs_commit,$rhs_commit";
            next if($bad{$k});
            next if($good{$k});

            if(is_bad($lhs_commit, $rhs_commit))
            {
                fill(\%bad, \%min_bad, \%parent_child_1, \%lhs_commits, \%rhs_commits, $lhs_commit, $rhs_commit);
                $min_bad{$k} = [$lhs_commit, $rhs_commit];
            }
            else
            {
                fill(\%good, undef, \%child_parent_1, \%lhs_commits, \%rhs_commits, $lhs_commit, $rhs_commit);
            }
        }
    }

    for my $min_bad (values(%min_bad))
    {
        print "Minimum conflict: " . $min_bad->[0] . ", " . $min_bad->[1] . "\n";
    }
}

sub is_bad
{
    my $lhs_commit = shift;
    my $rhs_commit = shift;

    my $mhs_commit;
    {
        open(my $fh, '-|', 'git', 'merge-base', $lhs_commit, $rhs_commit) || die "Cannot open git merge-base $lhs_commit $rhs_commit: $!";
        my $line = <$fh> || die;
        if($line =~ /^([0-9a-f]{40})$/)
        {
            $mhs_commit = $1;
        }
        else
        {
            die;
        }
        close($fh) || die "Cannot close git merge-base $lhs_commit $rhs_commit: $!";
    }

    {
        my $tempdir = tempdir('CLEANUP' => 1);

        my $wt = "$tempdir/wt";
        mkdir $wt;
        local $ENV{'GIT_WORK_TREE'} = $wt;

        my $index = "$tempdir/index";
        local $ENV{'GIT_INDEX_FILE'} = $index;

        (system('git', 'read-tree', '-im', $mhs_commit, $lhs_commit, $rhs_commit) == 0) || die "Could not system git read-tree -im ...: $!";
        return system('perl', '-e', 'open(STDOUT, ">/dev/null"); open(STDERR, ">/dev/null"); exec(@ARGV);', 'git', 'merge-index', 'git-merge-one-file', '-a') != 0;
    }
}

sub fill
{
    my $already = shift;
    my $already_min = shift;
    my $closure = shift;
    my $lhs_commits = shift;
    my $rhs_commits = shift;
    my $lhs_commit = shift;
    my $rhs_commit = shift;

    my $k = "$lhs_commit,$rhs_commit";

    delete($already_min->{$k}) if(defined($already_min));

    return if($already->{$k});
    $already->{$k} = 1;

    for my $lhs_commit2 ($lhs_commit, keys(%{$closure->{$lhs_commit}}))
    {
        next unless($lhs_commits->{$lhs_commit2});
        for my $rhs_commit2 ($rhs_commit, keys(%{$closure->{$rhs_commit}}))
        {
            next unless($rhs_commits->{$rhs_commit2});
            next if($lhs_commit2 eq $lhs_commit && $rhs_commit2 eq $rhs_commit);
            fill($already, $already_min, $closure, $lhs_commits, $rhs_commits, $lhs_commit2, $rhs_commit2);
        }
    }
}
