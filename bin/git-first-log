#!/usr/bin/perl

# Copyright (C) 2010   Keith Amling, keith.amling@gmail.com
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

$| = 1;

use strict;
use warnings;

use FindBin;
use lib "$FindBin::RealBin/../libs";

use Amling::Git::Utils;
use Getopt::Long;

my @minus_options;

my @options =
(
    "minus=s" => \@minus_options,
);

GetOptions(@options) || die;

for my $arg (@ARGV)
{
    my %h;
    my $cb = sub
    {
        my $h = shift;
        $h{$h->{'hash'}} = $h;
    };
    Amling::Git::Utils::log_commits([(map { "^" . $_ } @minus_options), $arg], $cb);

    my @lines;
    dump_commits(\@lines, '', {}, \%h, Amling::Git::Utils::convert_commitlike($arg), '0000000000000000000000000000000000000000');

    print map { "$_\n" } reverse(@lines);
}

sub dump_commits
{
    my $lines = shift;
    my $indent = shift;
    my $already = shift;
    my $data = shift;
    my $commit = shift;
    my $free_parent = shift;

    if($commit eq $free_parent)
    {
        return;
    }

    if(!$data->{$commit})
    {
        # TODO: stop being lazy and get subject
        push @$lines, $indent . '(-)' . substr($commit, 0, 12);
        return;
    }

    my $prefix;
    if($already->{$commit})
    {
        $prefix = '(v)';
    }
    else
    {
        $prefix = '';
        my @parents = @{$data->{$commit}->{'parents'}};
        if(@parents == 1)
        {
            dump_commits($lines, $indent, $already, $data, $parents[0], $free_parent);
        }
        elsif(@parents == 2)
        {
            dump_commits($lines, $indent, $already, $data, $parents[0], $free_parent);
            dump_commits($lines, "$indent  ", $already, $data, $parents[1], $parents[0]);
        }
        else
        {
            die;
        }

        $already->{$commit} = 1;
    }

    push @$lines, $indent . $prefix . substr($commit, 0, 12) . ' ' . $data->{$commit}->{'subj'};
}
